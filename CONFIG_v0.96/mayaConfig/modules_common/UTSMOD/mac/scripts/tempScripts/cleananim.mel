// CLEAN ANIM PROC

// removes any turn greater than 180 degrees from baked keys between frames $start and $end (inclusive). This prevents the appearence of motion blur on near 360 degree turns.

global proc cleananim(string $flock, int $n, int $start, int $end) {
	float $prevry[];
	int $frame;
	// run through given frame range
	for ($frame = $start; $frame<=$end; $frame++) {
		currentTime -e $frame;
		int $i;
		// run through boids
		for ($i=0; $i<$n; $i++) {
			$ryname = ($flock+"_boid_marker_"+$i+".ry");
			$ry = `getAttr $ryname`;
			float $diff = $ry-$prevry[$i];
			// if not first frame and change in angle is too big...
			if ($frame != $start && `abs($diff)`>180) {
				// calculate new angle
				while (`abs($diff)` > 180) {
					if ($diff>180)
						$diff -= 360;
					else if ($diff<-180)
						$diff += 360;
				}
				$ry = $prevry[$i] + $diff;
				// and set key
				keyframe -e -iub false -t $frame -a -o over -vc $ry $ryname ;
			}
			$prevry[$i] = $ry;
		}
	}
}

global proc cleananimwithstep(string $flock, int $n, int $start, int $end, int $step) {
	float $prevry[];
	int $frame;
	// run through given frame range
	for ($frame = $start; $frame<=$end; $frame+= $step) {
		currentTime -e $frame;
		int $i;
		// run through boids
		for ($i=0; $i<$n; $i++) {
			$ryname = ($flock+"_boid_marker_"+$i+".ry");
			$ry = `getAttr $ryname`;
			float $diff = $ry-$prevry[$i];
			// if not first frame and change in angle is too big...
			if ($frame != $start && `abs($diff)`>180) {
				// calculate new angle
				while (`abs($diff)` > 180) {
					if ($diff>180)
						$diff -= 360;
					else if ($diff<-180)
						$diff += 360;
				}
				$ry = $prevry[$i] + $diff;
				// and set key
				keyframe -e -iub false -t $frame -a -o over -vc $ry $ryname ;
			}
			$prevry[$i] = $ry;
		}
	}
}
