// select the top node of the chain
// and for each joint there will be a small construction
// of locators - the parent is constrained to the joint
// while the child is free to be offset

proc makeTrackers(string $rootNode) {
	select -r $rootNode;	
	string $childs[] = `listRelatives -children -fullPath -type joint 
	-allDescendents $rootNode`;
	int $num = `size($childs)`;
	$childs[$num]=$rootNode;
	
	if (!`size($childs)`) {
		print("Nothing selected\n");
		return;
	}
	string $child;
	for( $child in $childs ){
		select -r $child;		
		string $selectedBones[] = `ls -sl`;
		string $name = $selectedBones[0];
		spaceLocator -p 0 0 0 -name ($name + "LTracker");
		xform -s 0.1 0.1 0.1 ($name + "LTracker");
		polyCube -w 1 -h 1 -d 1 -sx 1 -sy 1 -sz 1 -ax 0 1 0 -tx 1 -ch 1 
		-n ($name + "Offset");
		spaceLocator -p 0 0 0 -name ($name + "LOffset");
		xform -s 0.1 0.1 0.1 ($name + "LOffset");
		parent -a ($name + "Offset") ($name + "LTracker");
		parent -a ($name + "LOffset") ($name + "Offset");
		pointConstraint -offset 0 0 0 -weight 1 $child ($name + "LTracker") ;
		orientConstraint -weight 1 $child ($name + "LTracker");
		
		pointConstraint -weight 1 ($name + "LTracker") ("Offset" + $name);
		orientConstraint -weight 1 ($name + "LTracker") ("Offset" + $name);
	}
}

proc makeOffsetSkeleton(string $rootNode) {
	select -r $rootNode;
//	if (!`namespace -add "OFFSET"`);
//	namespace -set "OFFSET";
	duplicate -rr $rootNode;
	string $copy[] = `ls -sl`; 	
	string $childs[] = `listRelatives -children -fullPath -type joint 
	-allDescendents $copy[0]`;
	int $num = `size($childs)`;
	$childs[$num]=$copy[0];	
	if (!`size($childs)`) {
		print("Nothing selected\n");
		return;
	}
	string $child;
	for( $child in $childs ){
		select -r $child;
		print("$child === " + $child + "\n");
		string $selectedBones[] = `ls -sl`;
		print("$selectedBones === " + $selectedBones[0] + "\n");
		string $name = $selectedBones[0];
		rename $child ("Offset"+$name);
	}
}

global proc setUpOffsetSkeleton() {

	string $selection[] = `ls -sl -type "joint"`;
		if (`size($selection)` == 0) {
		print("Nothing selected\n");
		return;
	}
	
	string $rootNode = $selection[0];
	makeOffsetSkeleton($rootNode);
	makeTrackers($rootNode);
	

}
