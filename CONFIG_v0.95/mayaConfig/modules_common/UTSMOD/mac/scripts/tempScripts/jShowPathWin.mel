/*jShowPathWin	make degree 1 curves out of the path of the selected
		object's travel with one knot per frame.		
		pick as many objects as you want -jpm
		

*/


global proc jShowPathWin () {
	global string $gTimeRangeSlider;
	string $win = `window`;
	columnLayout;
	int $startFrame = `playbackOptions -q -min`;
	int $endFrame  = `playbackOptions -q -max`;
	int $minRange = `rangeControl -q -minRange  $gTimeRangeSlider`;
	int $maxRange = `rangeControl -q -maxRange  $gTimeRangeSlider`;
	
	string $startFrameSlider = `intSliderGrp -label "Start Frame" 
		-min $minRange
		-max ($maxRange - 1)
		-field true 
		-v $startFrame`;

	string $endFrameSlider = `intSliderGrp -label "End Frame" 
		-min ($minRange + 1)
		-max $maxRange
		-field true 
		-v $endFrame`;
		
	rowLayout -nc 2;
		button -l Build 
		-c ("int $startEndDegree[] = `jGetUIVals " + 
		$startFrameSlider + " " +
		$endFrameSlider + "`; " + 
		"jShowPath $startEndDegree;");
		button -l Cancel -c ("deleteUI " + $win + ";");
	showWindow;
}	

global proc int[] jGetUIVals (string $startFrameSlider, string $endFrameSlider) {
	int $result[];
	 $result[0] = `intSliderGrp -q -v $startFrameSlider`;
	 $result[1] = `intSliderGrp -q -v $endFrameSlider`;
	 return $result;
}

global proc jShowPath (int $startEndDegree[]) {
// $startEndDegree is an array holding startFrame, endFrame, and degree UI values
	print "GOT HERE\n";
	print $startEndDegree;
	string $selected[] = `ls -sl`;
	// if start greater than end then swap
	if ($startEndDegree[0] > $startEndDegree[1]) {
		int $tmp = $startEndDegree[0];
		$startEndDegree[0] = $startEndDegree[1];
		$startEndDegree[1] = $tmp;
	}
	string $cmd[];
	string $loc[];
	for ($i = 0; $i < `size $selected`; $i++) {
		
		$cmd[$i] = ("curve -d 1 -ws ");
		$loc[$i] = `createNode locator`;
		string $locParents[] = `listRelatives -p $loc[$i]`;
		$loc[$i] = $locParents[0];
		pointConstraint $selected[$i] $loc[$i];
	}
	for ($j =  $startEndDegree[0]; $j <=  $startEndDegree[1]; $j ++) {
		currentTime  $j;
		for ($i = 0; $i < `size $selected`; $i++) {
			float $pos[] = `getAttr  ($loc[$i] + ".translate")`;
			$cmd[$i] = ($cmd[$i] + " -p " + $pos[0] + " " + $pos[1] + " " + $pos[2] + " ");
		}
	}
	for ($i = 0; $i < `size $selected`; $i++) {
		$cmd[$i] = ($cmd[$i] + " -n " +  $selected[$i] + "Path_" +  $startEndDegree[0] + "_" + $startEndDegree[1]);
		eval $cmd[$i];
		delete   $loc[$i];
	}
	select $selected;
}
