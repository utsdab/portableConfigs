//
// Written by Andrew Chapman - June 2003
//

proc int isRightWeaponChannel(string $channelName)
{
  string $rightProps[] = troyAgentPropsList(1,0);
  return (stringArrayCount($channelName, $rightProps) != 0);
}
 
proc int isLeftWeaponChannel(string $channelName)
{
  string $leftProps[] = troyAgentPropsList(2,0);
  return (stringArrayCount($channelName, $leftProps) != 0);
}

global proc troyAgentLinkMocapToSkeleton(string $agentTransformNode, string $agentShapeNode)
{
  string $rotations[]    = `agent -rn $agentShapeNode`;
  string $translations[] = `agent -tn $agentShapeNode`;
  
  // hook up bone rotations
  string $skeletonBones[] = `listRelatives -ad -fullPath -type joint $agentTransformNode`;
  int $i;
  for($i=0; $i<size($rotations); $i++) {
    int $j;
    int $found = 0;
    for($j=0; $j<size($skeletonBones); $j++) {
      if (`gmatch $skeletonBones[$j] ("*|" + $rotations[$i])`) {
        connectAttr -force ($agentShapeNode + ".mocapRotate[" + $i + "]") ($skeletonBones[$j] + ".rotate");
        $found = 1;
        break;
      }
    }
  }

  // connect weapons hook rotations and visibility
  string $allKids[]     = `listRelatives -allDescendents ($agentTransformNode + "|skeleton")`;
  string $allKidsFull[] = `listRelatives -allDescendents -fullPath ($agentTransformNode + "|skeleton")`;
  string $kid;
  int $i = 0;
  for($kid in $allKids) {
    if ($kid == "left_hand_hook") {
      int $j;
      for($j=0; $j<size($rotations); $j++) {
        if (isLeftWeaponChannel($rotations[$j])) {
          connectAttr -force ($agentShapeNode + ".mocapRotate[" + $j + "]") ($allKidsFull[$i] + ".rotate");
        }
      }
      for($j=0; $j<size($translations); $j++) {
        if (isLeftWeaponChannel($translations[$j])) {
          connectAttr -force ($agentShapeNode + ".mocapTranslate[" + $j + "]") ($allKidsFull[$i] + ".translate");
        }
      }
      connectAttr ($agentShapeNode + ".showWeapons") ($allKidsFull[$i] + ".visibility");
    } else if ($kid == "right_hand_hook") {
      int $j;
      for($j=0; $j<size($rotations); $j++) {
        if (isRightWeaponChannel($rotations[$j])) {
          connectAttr -force ($agentShapeNode + ".mocapRotate[" + $j + "]") ($allKidsFull[$i] + ".rotate");
        }
      }
      for($j=0; $j<size($translations); $j++) {
        if (isRightWeaponChannel($translations[$j])) {
          connectAttr -force ($agentShapeNode + ".mocapTranslate[" + $j + "]") ($allKidsFull[$i] + ".translate");
        }
      }
      connectAttr ($agentShapeNode + ".showWeapons") ($allKidsFull[$i] + ".visibility");
    }
    $i++;
  }
    
}
